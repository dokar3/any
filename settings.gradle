pluginManagement {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}
plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '1.0.0'
}

dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        mavenLocal()
        google()
        mavenCentral()
        maven {url 'https://oss.sonatype.org/content/repositories/snapshots/'}
        maven { url "https://www.jitpack.io" }
    }
}

enableFeaturePreview("TYPESAFE_PROJECT_ACCESSORS")

rootProject.name = "Any"

file("android").eachDir { dir ->
    if (dir.name == "ui") {
        include ":ui"
        project(":ui").projectDir = dir
        dir.eachDir { subDir ->
            include ":ui:${subDir.name}"
            project(":ui:${subDir.name}").projectDir = subDir
        }
    } else {
        include dir.name
        project(":${dir.name}").projectDir = dir
    }
}
