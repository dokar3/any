import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.dsl.KotlinVersion

plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.androidx.baselineprofile)
    alias(libs.plugins.compose.compiler)
}

kotlin {
    compilerOptions {
        jvmTarget = JvmTarget.fromTarget("17")
        languageVersion = KotlinVersion.KOTLIN_2_3
        apiVersion = KotlinVersion.KOTLIN_2_3
    }
}

android {
    namespace 'com.dokar.any'
    compileSdk libs.versions.compileSdk.get().toInteger()
    buildToolsVersion "36.0.0"

    defaultConfig {
        applicationId "com.dokar.any"
        minSdk 24
        targetSdk libs.versions.targetSdk.get().toInteger()
        versionCode 18
        versionName "0.6.9"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        release {
            Properties properties = new Properties()
            def localProps = rootProject.file('local.properties')
            if (localProps.exists()) {
                properties.load(localProps.newDataInputStream())
            }
            def path = envOrProp(properties, 'KEYSTORE_PATH')
            if (path == null) {
                return
            }
            def keystoreFile = rootProject.file(path)
            if (!keystoreFile.exists()) {
                keystoreFile = file(path)
            }
            if (keystoreFile.exists()) {
                storeFile keystoreFile
                storePassword envOrProp(properties, 'KEYSTORE_PASSWORD')
                keyAlias 'any'
                keyPassword envOrProp(properties, 'KEYSTORE_KEY_PASSWORD')
            }
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.release
            buildConfigField "boolean", "BENCHMARK", "false"
        }
        benchmark {
            signingConfig signingConfigs.release
            matchingFallbacks = ['release']
            minifyEnabled true
            debuggable false
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro', 'benchmark-rules.pro'
            buildConfigField "boolean", "BENCHMARK", "true"
        }
        release {
            // Disable minify when running a compiler reports release build
            minifyEnabled project.findProperty("enableComposeCompilerReports") != "true"
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "BENCHMARK", "false"
        }
    }

    splits {
        abi {
            enable !project.hasProperty("disableSplits")
            reset()
            include "arm64-v8a", "x86_64"
            universalApk true
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    buildFeatures {
        compose true
        buildConfig true
    }
}

static def envOrProp(Properties props, String propName) {
    def envVal = System.getenv(propName)
    return envVal != null ? envVal : props[propName]
}

dependencies {
    baselineProfile(projects.macrobenchmark)

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'

    implementation projects.base
    implementation projects.data
    implementation projects.domain
    implementation projects.download
    implementation projects.navigation
    implementation projects.commonUi
    implementation projects.ui.home
    implementation projects.ui.search
    implementation projects.ui.service
    implementation projects.ui.post
    implementation projects.ui.profile
    implementation projects.ui.imagepager
    implementation projects.ui.settings
    implementation projects.ui.password
    implementation projects.ui.runsql
    implementation projects.ui.jslogger
    implementation projects.ui.readingbubble

    implementation libs.sheets

    implementation libs.compose.ui.tooling
    implementation libs.androidx.lifecycle.runtime
    implementation libs.androidx.profileinstaller

    testImplementation libs.junit
    androidTestImplementation libs.androidx.test.junit
    androidTestImplementation libs.androidx.test.espresso
}

baselineProfile {
    saveInSrc true
}

apply from: "./build_services.gradle"
