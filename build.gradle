import org.jetbrains.kotlin.gradle.tasks.KotlinCompilationTask

plugins {
    alias(libs.plugins.android.application) apply false
    alias(libs.plugins.android.library) apply false
    alias(libs.plugins.android.test) apply false
    alias(libs.plugins.kotlin.android) apply false
    alias(libs.plugins.ksp) apply false
    alias(libs.plugins.androidx.baselineprofile) apply false
}

subprojects {
    tasks.withType(KotlinCompilationTask).configureEach {
        compilerOptions {
            // Avoid compile error when implementing a ViewModelProvider.Factory
            // https://issuetracker.google.com/issues/217593040
            freeCompilerArgs.addAll(
                    "-Xjvm-default=all",
                    "-opt-in=kotlin.RequiresOptIn",
            )

            if (project.findProperty("enableComposeCompilerReports") == "true") {
                freeCompilerArgs.addAll(
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:reportsDestination=" +
                                project.buildDir.absolutePath + "/compose_metrics",
                )
                freeCompilerArgs.addAll(
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:metricsDestination=" +
                                project.buildDir.absolutePath + "/compose_metrics",
                )
            }
        }
    }
}
